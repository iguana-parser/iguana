
global env = set()

start Definition
  = (Rule | Global)+

Global
 = "global" id:Identifier "=" Expression  env=put(env,id.yield)

Rule
  = ("start" | "layout")? Name Parameters? "=" Body        %Syntax
  | "layout"? "terminal" Name "=" RegexBody                %Lexical

Parameters
  = "(" {id:Identifier env=put(env, id.yield) ","}* ")"

RegexRule
  = NontName ":" RegexBody    %Regex

Body
  = {PriorityLevels ">"}+

PriorityLevels
  = {Alternative "|"}+  %Prec

Alternative
  = Sequence                                         %Sequence
  | Associativity "(" Sequence ("|" Sequence)+ ")"   %Assoc
  | Label?                                           %Empty

RegexBody
  = {RegexSequence "|"}*

Sequence
  = Associativity? PreCondition? Symbol Symbol+ ReturnExpression? Label?   %MoreThanOne
  | PreCondition? Symbol ReturnExpression? Label?                          %Single

PreCondition
  = "[[" {Expression ","}* "]]"

RegexSequence
  = Regex+    %Sequence

Symbol
  = Identifier Arguments                    %Call
  > "offside" Symbol                        %Offside
  > Symbol "*"                              %Star
  | Symbol "+"                              %Plus
  | Symbol "?"                              %Option
  | "(" Symbol Symbol* ")"                  %Sequence
  | "(" Symbol+ ("|" Symbol+)+ ")"          %Alternation
  > "align" Symbol                          %Align
  | "ignore" Symbol                         %Ignore
  > Identifier ":" Symbol                   %Labeled
  > Symbol!Statement Statement+             %Statement
  | Symbol "[[" {Expression ","}* "]]"      %PostCondition
  > Regex "<<" Symbol                       %Precede
  | Regex "!<<" Symbol                      %NotPrecede
  > Symbol ">>" Regex                       %Follow
  | Symbol "!>>" Regex                      %NotFollow
  | Symbol "\\" Regex                       %Exclude
  | Symbol "!" Identifier                   %Except
  | "if" Expression Symbol "else" Symbol    %IfThenElse
  | Identifier                              %Nont
  | String                                  %String
  | Char                                    %Character
  | "{" Symbol Symbol+ "}" "*"              %StarSep
  | "{" Symbol Symbol+ "}" "+"              %PlusSep

Arguments
  = "(" {Expression ","}* ")"

Statement
  = FunName Arguments ";"?    %Call
  | Binding ";"?              %Binding

Binding
  = VarName "=" Expression                                            %Assign
  | "var" {(id:Name env=put(env, id.yield) "=" Expression) ","}+      %Declare

Regex
  = Regex "*"                     %Star
  | Regex "+"                     %Plus
  | Regex "?"                     %Option
  | "(" Regex ")"                 %Bracket
  | "(" Regex Regex+ ")"          %Sequence
  | "(" Regexs ("|" Regexs)+ ")"  %Alternation
  | NontName                      %Nont
  | CharClass                     %CharClass
  | String                        %String
  | Char                          %Character

Regexs
  = Regex+  %Sequence

CharClass
  = "[" Range* "]"      %Chars
  | "!" "[" Range* "]"  %NotChars

Range
  = RangeChar "-" RangeChar   %Range
  | RangeChar                 %Character

Expression
  =           FunName Arguments            %Call
  |           "!" Expression               %Not
  > left      (Expression "*" Expression   %Multiplication
  |            Expression "/" Expression   %Division)
  > left      (Expression "+" Expression   %Addition
  |            Expression "-" Expression   %Subtraction)
  > non-assoc (Expression ">=" Expression  %GreaterEq
  |            Expression "<=" Expression  %LessEq
  |            Expression ">" Expression   %Greater
  |            Expression "<" Expression   %Less)
  > non-assoc (Expression "==" Expression  %Equal
  |            Expression "!=" Expression  %NotEqual)
  > left      (Expression "&&" Expression  %And
  |            Expression "||" Expression  %Or)
  |           Identifier ".l"              %LExtent
  |           Identifier ".r"              %RExtent
  |           Identifier ".yield"          %Yield
  |           Identifier ".val"            %Val
  |           VarName                      %Name
  |           Number                       %Number
  |           "(" Expression ")"           %Bracket

ReturnExpression
  = "{" Expression "}"    %ReturnExpression

terminal Letter
  = [A-Za-z_]

terminal LetterOrDigit
  = [A-Za-z_0-9]

terminal LetterOrDigits
  = Letter (Letter | LetterOrDigit)*

terminal Number
  = [0]
  | [1-9][0-9]*

terminal Character
  = "\\" [' " \\ t f r n]
  | ![' " \\]

terminal RangeChar
  = "\\" [\\ \[ \] \- t f r n \ ]
  | ![\\ \[ \] \- \ ]

terminal Char
  = "\'" Character* "\'"

terminal String
  = "\"" Character* "\""

layout terminal Layout
  = WhiteSpaceOrComment*

terminal WhiteSpaceOrComment
  = WhiteSpaces
  | Comment

terminal WhiteSpaces
  = [\ \t \f \r \n]+

terminal Comment
  = "/*" CommentChar* [*]+  "/"
  | "//" ![\r \n]* [\r\n]

terminal CommentChar
  = [/]
  | [*]* ![/ *]

Identifier
  = [A-Z_a-z] !<< LetterOrDigits \ Keywords %Ident

terminal Associativity
  = "left"
  | "right"
  | "non-assoc"

VarName
  = id:Identifier [[contains(env,id.yield)]]

Label
  = '%' Identifier

Name
  = id:Identifier   [[!contains(env,id.yield)]]

NontName
  = Identifier

terminal Keywords
  = "start"
  | "terminal"
  | "layout"
  | "var"
  | "left"
  | "right"
  | "non-assoc"
  | "align"
  | "offside"
  | "ignore"
  | "println"
  | "indent"
  | "if"
  | "else"
  | "assert"
  | "set"
  | "contains"
  | "put"

terminal FunName
  = "println"
  | "indent"
  | "assert"
  | "set"
  | "contains"
  | "put"
